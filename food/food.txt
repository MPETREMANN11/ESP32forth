\ *********************************************************************
\ FOOD Forth Object Oriented Dialect
\    Filename:      food.txt
\    Date:          14 feb 2022
\    Updated:       14 feb 2022
\    File Version:  1.0
\    MCU:           ESP32-WROOM-32
\    Forth:         ESP32forth all versions 7.x++
\    Copyright:     Marc PETREMANN / Michel ZUPAN
\    Author:        Michel ZUPAN sep 1989
\    Adaptation:    Marc PETREMANN
\    GNU General Public License
\ *********************************************************************

\ FOOD  Forth Object-Oriented Dialect
\ Version zéro 9/89 Michel ZUPAN

DEFINED? --food [IF] forget --food  [THEN]
create --food

DEFINED? off invert [IF] 
: off ( addr -- )
    0 swap !  ;
[THEN]

DEFINED? off invert [IF] 
: ?dup ( n -- n | n n )
    dup if dup then  ;
[THEN]

variable Active                         \ pointeur classe active
variable ClassLink                      \ lien dernière classe définie


\ Classes, sub-classes, super-classes
\ ------------------------------------

\ define sub-class of active class
: subclass   ( comp: -- <class-name> | exec: -- )      
    create 
        here Active    dup @ , !
        here ClassLink dup @ , !
        immediate
    does> 
        Active ! 
  ;

\ define new class
: class      ( comp: -- <class-name> | exec: -- )       
    Active off 
    subclass 
  ;

\ define super class of active class
: superclass ( <nom-classe> --- )
    Active @ 
    class 
    Active @ swap ! 
  ;


\ Classes management
\ -------------------

\ display name field address of a word
: .id ( nfa -- )
    type space 
  ;

\ class2 sub-class of class1 ?
: subclass? ( class1 class2 -- fl)      
    begin 
        2dup = 2dup 0= and 
    while 
        drop @ 
    repeat 
    -rot 2drop 
  ;

\ display list of defined classes
: classes   ( --- )
    ClassLink
    begin 
        @ ?dup 
    while 
        dup 6 - >name .id  ( @TODO: bug to correct here )
    repeat 
  ;

\ display active class
: .active   ( --- )                     
    Active @ ?dup 
    if 
        body> >name .id  ( @TODO: bug to correct here )
    else ." no class " 
    then
        ." active class " 
  ;
