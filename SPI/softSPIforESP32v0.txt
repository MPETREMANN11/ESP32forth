\ *********************************************************************
\ Soft SPI emulation for ESP32 card
\    Filename:      softSPIforESP32.txt
\    Date:          03 july 2022
\    Updated:       17 july 2022
\    File Version:  0.0
\    MCU:           ESP32-WROOM-32
\    Forth:         ESP32forth all versions 7.x++
\    Copyright:     ohiyooo2 / Frank Lin
\    Original publ. https://ohiyooo2.pixnet.net/blog/post/406059107
\    Adaptation for GitHub: Marc PETREMANN
\    GNU General Public License
\ *********************************************************************

DEFINED? --softSPI [if] forget --softSPI  [then]
create --softSPI

\
\  ESP32FORTH mini Banner
\    Frank Lin 2022.07.03
\

: chars    ; immediate
: char+ 1+ ;
: delay ms ;

: ->HIGH    ( pin --)  HIGH digitalWrite ;
: ->LOW     ( pin --)  LOW  digitalWrite ;
: WaitTrans ( pin --)  digitalRead drop ;

32 constant DATA   ( SPI MOSI)
33 constant CS     ( SPI Enable)
25 constant CLOCK  ( SPI SCLK)

\
\ software SPI
\

hex

80 constant MASK

: ByteSend ( data --)
    8 0
    do   dup MASK and
        if    DATA ->HIGH
        else  DATA ->LOW   then

        CLOCK ->HIGH
        1 LSHIFT

        CLOCK WaitTrans
        CLOCK ->LOW
   loop drop
;

: Send ( data register --)
   CS ->LOW
   ByteSend
   ByteSend
   CS ->HIGH

   CS    WaitTrans
   CLOCK WaitTrans
;

\
\ MAX 7219 control
\

\ MAX 7219 registers

0 constant NO_OP
9 constant DECODE_MODE
A constant INTENSITY
B constant SCAN_LIMIT
C constant SHUTDOWN
F constant DISPLAY_TEST

: init7219
   DATA   OUTPUT pinMode
   CLOCK  OUTPUT pinMode
   CS     OUTPUT pinMode

   DATA  ->LOW
   CLOCK ->LOW
   CS    ->HIGH

   0      NO_OP        Send
   1      SHUTDOWN     Send
   0      DISPLAY_TEST Send
   1      INTENSITY    Send
   7      SCAN_LIMIT   Send
   0      DECODE_MODE  Send
;


\ screen buffer mapped to 7219 registers of 8x8 LED matrix

create ScreenBuffer 8 chars allot

 64 value   PausedTime    \ control the speed of scrolling
  0 value  'fonts         \ pointer pointed to the address of the font data


: UpdateScreen  \ update buffer data to 7219
   8 0
   do  ScreenBuffer i chars + c@
       i 1+
       Send
   loop
;


\ scrolling data inside screen buffer

: ScrollScreen ( P.addr --) \ P.addr is the address of your 8x8 picture
    8 0
    do   ( P.addr)
       ( move one line forward inside screen buffer)
       ScreenBuffer char+    ScreenBuffer   7   cmove

       ( supply new data from P.addr to the end of screen buffer)
       dup c@   ScreenBuffer 7 chars +     c!

       char+  ( step P.addr)

       UpdateScreen   PausedTime delay  ( refresh 8x8 LED Matrix)

    loop  drop
;


\ scroll a character

: ScrollChar ( char --)
    0 max 127 min         ( range check)
    8 chars *  'fonts  +  ( calc actual address of font)
    ScrollScreen
;

\
\  Syntax Examples:
\
\  S" Happy Birthday Hayley Westenra" Scrolling
\

\ scroll a counted string

: Scrolling ( addr n --)
    0 do dup i chars + c@ ScrollChar
      loop drop
;

\ Let's have some funs

: p  1000 delay ;

: Hana
   init7219
   begin
      S" Hana - Hayley Westenra "                Scrolling   p
      S" I see the river in the daylight "       Scrolling   p
      S" Glistening as it flows its way "        Scrolling   p
      S" I see the people travelling "           Scrolling   p
      S" Through the night and through the day " Scrolling   p
      S" I see their paths colliding "           Scrolling   p
      S" Water drops and golden rays "           Scrolling   p
      S" Flowers bloom, oh, flowers bloom "      Scrolling   p
      S" On this blessed day "                   Scrolling   p
      S" Let the tears fall back "               Scrolling   p
      S" Let the laughter through "              Scrolling   p
      S" One day, oh, one day "                  Scrolling   p
      S" The Flowers will reach full bloom "     Scrolling   p
      p p p
   again
;

\ 127 fonts data for 8x8 LED Matrix
\ the order is as in ascii code

create fonts
   00 c, 00 c, 00 c, 00 c, 00 c, 00 c, 00 c, 00 c, \ 00
   7E c, 81 c, 95 c, B1 c, B1 c, 95 c, 81 c, 7E c, \ 01
   7E c, FF c, EB c, CF c, CF c, EB c, FF c, 7E c, \ 02
   0E c, 1F c, 3F c, 7E c, 3F c, 1F c, 0E c, 00 c, \ 03
   08 c, 1C c, 3E c, 7F c, 3E c, 1C c, 08 c, 00 c, \ 04
   18 c, BA c, FF c, FF c, FF c, BA c, 18 c, 00 c, \ 05
   10 c, B8 c, FC c, FF c, FC c, B8 c, 10 c, 00 c, \ 06
   00 c, 00 c, 18 c, 3C c, 3C c, 18 c, 00 c, 00 c, \ 07
   FF c, FF c, E7 c, C3 c, C3 c, E7 c, FF c, FF c, \ 08
   00 c, 3C c, 66 c, 42 c, 42 c, 66 c, 3C c, 00 c, \ 09
   FF c, C3 c, 99 c, BD c, BD c, 99 c, C3 c, FF c, \ 0A
   70 c, F8 c, 88 c, 88 c, FD c, 7F c, 07 c, 0F c, \ 0B
   00 c, 4E c, 5F c, F1 c, F1 c, 5F c, 4E c, 00 c, \ 0C
   C0 c, E0 c, FF c, 7F c, 05 c, 05 c, 07 c, 07 c, \ 0D
   C0 c, FF c, 7F c, 05 c, 05 c, 65 c, 7F c, 3F c, \ 0E
   99 c, 5A c, 3C c, E7 c, E7 c, 3C c, 5A c, 99 c, \ 0F
   7F c, 3E c, 3E c, 1C c, 1C c, 08 c, 08 c, 00 c, \ 10
   08 c, 08 c, 1C c, 1C c, 3E c, 3E c, 7F c, 00 c, \ 11
   00 c, 24 c, 66 c, FF c, FF c, 66 c, 24 c, 00 c, \ 12
   00 c, 5F c, 5F c, 00 c, 00 c, 5F c, 5F c, 00 c, \ 13
   06 c, 0F c, 09 c, 7F c, 7F c, 01 c, 7F c, 7F c, \ 14
   40 c, DA c, BF c, A5 c, FD c, 59 c, 03 c, 02 c, \ 15
   00 c, 70 c, 70 c, 70 c, 70 c, 70 c, 70 c, 00 c, \ 16
   80 c, 94 c, B6 c, FF c, FF c, B6 c, 94 c, 80 c, \ 17
   00 c, 04 c, 06 c, 7F c, 7F c, 06 c, 04 c, 00 c, \ 18
   00 c, 10 c, 30 c, 7F c, 7F c, 30 c, 10 c, 00 c, \ 19
   08 c, 08 c, 08 c, 2A c, 3E c, 1C c, 08 c, 00 c, \ 1A
   08 c, 1C c, 3E c, 2A c, 08 c, 08 c, 08 c, 00 c, \ 1B
   3C c, 3C c, 20 c, 20 c, 20 c, 20 c, 20 c, 00 c, \ 1C
   08 c, 1C c, 3E c, 08 c, 08 c, 3E c, 1C c, 08 c, \ 1D
   30 c, 38 c, 3C c, 3E c, 3E c, 3C c, 38 c, 30 c, \ 1E
   06 c, 0E c, 1E c, 3E c, 3E c, 1E c, 0E c, 06 c, \ 1F
   00 c, 00 c, 00 c, 00 c, 00 c, 00 c, 00 c, 00 c, \ ' '
   00 c, 06 c, 5F c, 5F c, 06 c, 00 c, 00 c, 00 c, \ '!'
   00 c, 07 c, 07 c, 00 c, 07 c, 07 c, 00 c, 00 c, \ '"'
   14 c, 7F c, 7F c, 14 c, 7F c, 7F c, 14 c, 00 c, \ '#'
   24 c, 2E c, 6B c, 6B c, 3A c, 12 c, 00 c, 00 c, \ '$'
   46 c, 66 c, 30 c, 18 c, 0C c, 66 c, 62 c, 00 c, \ '%'
   30 c, 7A c, 4F c, 5D c, 37 c, 7A c, 48 c, 00 c, \ '&'
   04 c, 07 c, 03 c, 00 c, 00 c, 00 c, 00 c, 00 c, \ '''
   00 c, 1C c, 3E c, 63 c, 41 c, 00 c, 00 c, 00 c, \ '('
   00 c, 41 c, 63 c, 3E c, 1C c, 00 c, 00 c, 00 c, \ ')'
   08 c, 2A c, 3E c, 1C c, 1C c, 3E c, 2A c, 08 c, \ '*'
   08 c, 08 c, 3E c, 3E c, 08 c, 08 c, 00 c, 00 c, \ '+'
   00 c, 80 c, E0 c, 60 c, 00 c, 00 c, 00 c, 00 c, \ ' c,'
   08 c, 08 c, 08 c, 08 c, 08 c, 08 c, 00 c, 00 c, \ '-'
   00 c, 00 c, 60 c, 60 c, 00 c, 00 c, 00 c, 00 c, \ '.'
   60 c, 30 c, 18 c, 0C c, 06 c, 03 c, 01 c, 00 c, \ '/'
   3E c, 7F c, 71 c, 59 c, 4D c, 7F c, 3E c, 00 c, \ '0'
   40 c, 42 c, 7F c, 7F c, 40 c, 40 c, 00 c, 00 c, \ '1'
   62 c, 73 c, 59 c, 49 c, 6F c, 66 c, 00 c, 00 c, \ '2'
   22 c, 63 c, 49 c, 49 c, 7F c, 36 c, 00 c, 00 c, \ '3'
   18 c, 1C c, 16 c, 53 c, 7F c, 7F c, 50 c, 00 c, \ '4'
   27 c, 67 c, 45 c, 45 c, 7D c, 39 c, 00 c, 00 c, \ '5'
   3C c, 7E c, 4B c, 49 c, 79 c, 30 c, 00 c, 00 c, \ '6'
   03 c, 03 c, 71 c, 79 c, 0F c, 07 c, 00 c, 00 c, \ '7'
   36 c, 7F c, 49 c, 49 c, 7F c, 36 c, 00 c, 00 c, \ '8'
   06 c, 4F c, 49 c, 69 c, 3F c, 1E c, 00 c, 00 c, \ '9'
   00 c, 00 c, 66 c, 66 c, 00 c, 00 c, 00 c, 00 c, \ ':'
   00 c, 80 c, E6 c, 66 c, 00 c, 00 c, 00 c, 00 c, \ ';'
   08 c, 1C c, 36 c, 63 c, 41 c, 00 c, 00 c, 00 c, \ '<'
   24 c, 24 c, 24 c, 24 c, 24 c, 24 c, 00 c, 00 c, \ '='
   00 c, 41 c, 63 c, 36 c, 1C c, 08 c, 00 c, 00 c, \ '>'
   02 c, 03 c, 51 c, 59 c, 0F c, 06 c, 00 c, 00 c, \ '?'
   3E c, 7F c, 41 c, 5D c, 5D c, 1F c, 1E c, 00 c, \ '@'
   7C c, 7E c, 13 c, 13 c, 7E c, 7C c, 00 c, 00 c, \ 'A'
   41 c, 7F c, 7F c, 49 c, 49 c, 7F c, 36 c, 00 c, \ 'B'
   1C c, 3E c, 63 c, 41 c, 41 c, 63 c, 22 c, 00 c, \ 'C'
   41 c, 7F c, 7F c, 41 c, 63 c, 3E c, 1C c, 00 c, \ 'D'
   41 c, 7F c, 7F c, 49 c, 5D c, 41 c, 63 c, 00 c, \ 'E'
   41 c, 7F c, 7F c, 49 c, 1D c, 01 c, 03 c, 00 c, \ 'F'
   1C c, 3E c, 63 c, 41 c, 51 c, 73 c, 72 c, 00 c, \ 'G'
   7F c, 7F c, 08 c, 08 c, 7F c, 7F c, 00 c, 00 c, \ 'H'
   00 c, 41 c, 7F c, 7F c, 41 c, 00 c, 00 c, 00 c, \ 'I'
   30 c, 70 c, 40 c, 41 c, 7F c, 3F c, 01 c, 00 c, \ 'J'
   41 c, 7F c, 7F c, 08 c, 1C c, 77 c, 63 c, 00 c, \ 'K'
   41 c, 7F c, 7F c, 41 c, 40 c, 60 c, 70 c, 00 c, \ 'L'
   7F c, 7F c, 0E c, 1C c, 0E c, 7F c, 7F c, 00 c, \ 'M'
   7F c, 7F c, 06 c, 0C c, 18 c, 7F c, 7F c, 00 c, \ 'N'
   1C c, 3E c, 63 c, 41 c, 63 c, 3E c, 1C c, 00 c, \ 'O'
   41 c, 7F c, 7F c, 49 c, 09 c, 0F c, 06 c, 00 c, \ 'P'
   1E c, 3F c, 21 c, 71 c, 7F c, 5E c, 00 c, 00 c, \ 'Q'
   41 c, 7F c, 7F c, 09 c, 19 c, 7F c, 66 c, 00 c, \ 'R'
   26 c, 6F c, 4D c, 59 c, 73 c, 32 c, 00 c, 00 c, \ 'S'
   03 c, 41 c, 7F c, 7F c, 41 c, 03 c, 00 c, 00 c, \ 'T'
   7F c, 7F c, 40 c, 40 c, 7F c, 7F c, 00 c, 00 c, \ 'U'
   1F c, 3F c, 60 c, 60 c, 3F c, 1F c, 00 c, 00 c, \ 'V'
   7F c, 7F c, 30 c, 18 c, 30 c, 7F c, 7F c, 00 c, \ 'W'
   43 c, 67 c, 3C c, 18 c, 3C c, 67 c, 43 c, 00 c, \ 'X'
   07 c, 4F c, 78 c, 78 c, 4F c, 07 c, 00 c, 00 c, \ 'Y'
   47 c, 63 c, 71 c, 59 c, 4D c, 67 c, 73 c, 00 c, \ 'Z'
   00 c, 7F c, 7F c, 41 c, 41 c, 00 c, 00 c, 00 c, \ '['
   01 c, 03 c, 06 c, 0C c, 18 c, 30 c, 60 c, 00 c, \ backslash
   00 c, 41 c, 41 c, 7F c, 7F c, 00 c, 00 c, 00 c, \ ']'
   08 c, 0C c, 06 c, 03 c, 06 c, 0C c, 08 c, 00 c, \ '^'
   80 c, 80 c, 80 c, 80 c, 80 c, 80 c, 80 c, 80 c, \ '_'
   00 c, 00 c, 03 c, 07 c, 04 c, 00 c, 00 c, 00 c, \ '`'
   20 c, 74 c, 54 c, 54 c, 3C c, 78 c, 40 c, 00 c, \ 'a'
   41 c, 7F c, 3F c, 48 c, 48 c, 78 c, 30 c, 00 c, \ 'b'
   38 c, 7C c, 44 c, 44 c, 6C c, 28 c, 00 c, 00 c, \ 'c'
   30 c, 78 c, 48 c, 49 c, 3F c, 7F c, 40 c, 00 c, \ 'd'
   38 c, 7C c, 54 c, 54 c, 5C c, 18 c, 00 c, 00 c, \ 'e'
   48 c, 7E c, 7F c, 49 c, 03 c, 02 c, 00 c, 00 c, \ 'f'
   98 c, BC c, A4 c, A4 c, F8 c, 7C c, 04 c, 00 c, \ 'g'
   41 c, 7F c, 7F c, 08 c, 04 c, 7C c, 78 c, 00 c, \ 'h'
   00 c, 44 c, 7D c, 7D c, 40 c, 00 c, 00 c, 00 c, \ 'i'
   60 c, E0 c, 80 c, 80 c, FD c, 7D c, 00 c, 00 c, \ 'j'
   41 c, 7F c, 7F c, 10 c, 38 c, 6C c, 44 c, 00 c, \ 'k'
   00 c, 41 c, 7F c, 7F c, 40 c, 00 c, 00 c, 00 c, \ 'l'
   7C c, 7C c, 18 c, 38 c, 1C c, 7C c, 78 c, 00 c, \ 'm'
   7C c, 7C c, 04 c, 04 c, 7C c, 78 c, 00 c, 00 c, \ 'n'
   38 c, 7C c, 44 c, 44 c, 7C c, 38 c, 00 c, 00 c, \ 'o'
   84 c, FC c, F8 c, A4 c, 24 c, 3C c, 18 c, 00 c, \ 'p'
   18 c, 3C c, 24 c, A4 c, F8 c, FC c, 84 c, 00 c, \ 'q'
   44 c, 7C c, 78 c, 4C c, 04 c, 1C c, 18 c, 00 c, \ 'r'
   48 c, 5C c, 54 c, 54 c, 74 c, 24 c, 00 c, 00 c, \ 's'
   00 c, 04 c, 3E c, 7F c, 44 c, 24 c, 00 c, 00 c, \ 't'
   3C c, 7C c, 40 c, 40 c, 3C c, 7C c, 40 c, 00 c, \ 'u'
   1C c, 3C c, 60 c, 60 c, 3C c, 1C c, 00 c, 00 c, \ 'v'
   3C c, 7C c, 70 c, 38 c, 70 c, 7C c, 3C c, 00 c, \ 'w'
   44 c, 6C c, 38 c, 10 c, 38 c, 6C c, 44 c, 00 c, \ 'x'
   9C c, BC c, A0 c, A0 c, FC c, 7C c, 00 c, 00 c, \ 'y'
   4C c, 64 c, 74 c, 5C c, 4C c, 64 c, 00 c, 00 c, \ 'z'
   08 c, 08 c, 3E c, 77 c, 41 c, 41 c, 00 c, 00 c, \ ''
   00 c, 00 c, 00 c, 77 c, 77 c, 00 c, 00 c, 00 c, \ '|'
   41 c, 41 c, 77 c, 3E c, 08 c, 08 c, 00 c, 00 c, \ ''
   02 c, 03 c, 01 c, 03 c, 02 c, 03 c, 01 c, 00 c, \ '~'

fonts to 'fonts

decimal
