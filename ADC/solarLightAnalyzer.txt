\ *********************************************************************
\ Solar light analyzer
\    Filename:      solarLightAnalyzer.txt
\    Date:          04 nov 2022
\    Updated:       05 nov 2022
\    File Version:  1.0
\    MCU:           ESP32-WROOM-32
\    Forth:         ESP32forth all versions 7.x++
\    Copyright:     Marc PETREMANN
\    Author:        Marc PETREMANN
\    Link:          https://esp32.arduino-forth.com/article/ADC_capteurSoleil
\    GNU General Public License
\ *********************************************************************

DEFINED? --SolarAnalyzer [if] forget --SolarAnalyzer  [then]
create --SolarAnalyzer

\ solar cell connected on pin G34
34 constant SOLAR_CELL

: init-solar-cell ( -- )
    SOLAR_CELL input pinMode
  ;

: solar-cell-read ( -- n )
    SOLAR_CELL analogRead
  ;

\ test loop, uncomment for use
\ : solar-cell-loop ( --)
\     init-solar-cell
\     begin
\         solar-cell-read cr .
\         200 ms
\     key? until
\   ;

17 constant DEVICE_ON      \ green LED
16 constant DEVICE_OFF     \   red LED

: init-device-state ( -- )
    DEVICE_ON  output pinMode
    DEVICE_OFF output pinMode
  ;

\ define trigger high state delay
500 value DEVICE_DELAY

\ set HIGH level of device
: device-activation { device -- }
    device HIGH digitalWrite
    DEVICE_DELAY ?dup
    if
        ms
        device LOW  digitalWrite
    then
  ;

\ define device state: 0=LOW, -1=HIGH
0 value DEVICE_STATE

\ enable device
: device-ON ( -- )
    DEVICE_STATE invert
    if
        DEVICE_OFF LOW  digitalWrite
        DEVICE_ON  device-activation
        -1 to DEVICE_STATE
    then
  ;

\ disable device
: device-OFF ( -- )
    DEVICE_STATE
    if
        DEVICE_ON  LOW  digitalWrite
        DEVICE_OFF device-activation
         0 to DEVICE_STATE
    then
  ;

\ define trigger value for sunny or cloudy sky
300 value SOLAR_TRIGGER

\ if solar light > SOLAR_TRIGGER, activate action
: action-light-level ( -- )
    solar-cell-read SOLAR_TRIGGER >=
    if
        device-ON
    else
        device-OFF
    then
  ;


init-solar-cell
init-device-state



