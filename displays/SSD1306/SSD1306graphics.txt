\ *********************************************************************
\ Graphics for OLED SSD1306 display
\    Filename:      SSD10306graphics.txt
\    Date:          20 feb 2022
\    Updated:       25 feb 2022
\    File Version:  1.0
\    MCU:           ESP32-WROOM-32
\    Forth:         ESP32forth all versions 7.x++
\    Copyright:     Marc PETREMANN
\    Author:        Marc PETREMANN
\    GNU General Public License
\ *********************************************************************

DEFINED? --oledGraphics [if] forget --oledGraphics  [then]
create --oledGraphics

\ include SSD1306commands.txt

DISPLAY_HEIGHT 8 /
DISPLAY_WIDTH *  constant oledBufferSize

create oledBuffer
    oledBufferSize  allot

: emptyOledBuffer ( -- )
    oledBuffer oledBufferSize 0 fill
  ;

\ retrieves the address of the buffer corresponding to the coordinates x y
: getXYaddrFromBuffer ( x y -- addr )
    8 /  128 *          \ determine page in OLED display
    swap +              \ calculate offset in this page
    oledBuffer +
  ;

: txBuffer ( -- )
    OledCLS
    DISPLAY_HEIGHT 8 / 0 do
        DISPLAY_WIDTH 32 / 0 do
            SSD1306beginTransmission
            CTRL_DATAS Wire.send
            oledBuffer j DISPLAY_WIDTH * +
            32 i * +
            32 Wire.write drop
            SSD1306endTransmission
        loop
    loop
  ;


0 value currentCOLOR

\ define word as COLOR constant
: color: ( n -- <name> )
    create
        ,
    does>
        @ to currentCOLOR
  ;

$00 color: setBLACK
$ff color: setWHITE

: plot ( x y -- )
    2dup getXYaddrFromBuffer
    -rot nip  8 mod
    1 swap lshift
    over c@ or
    swap c!
  ;


